plugins {
    id 'com.google.protobuf'
}

dependencies {
    implementation 'io.grpc:grpc-protobuf'
    implementation 'io.grpc:grpc-stub'
    if (JavaVersion.current().isJava9Compatible()) {
        // Workaround for @javax.annotation.Generated
        // see: https://github.com/grpc/grpc-java/issues/3633
        implementation 'javax.annotation:javax.annotation-api:1.3.1'
    }
}

protobuf {
    protoc {
        // Download protoc compiler
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
    generatedFilesBaseDir = "${projectDir}/src/gen"
    clean {
        delete generatedFilesBaseDir
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java"
        }
    }

    generateProtoTasks {
        all().each { Task task ->
            task.plugins {
                grpc {}
            }
            task.builtins {
                js {
                    option "import_style=commonjs"
                    option "binary"
                }
            }
        }
    }
}

idea {
    module {
        sourceDirs += file('src/gen/main/java')
        sourceDirs += file('src/gen/main/grpc')
        generatedSourceDirs += file('src/gen/main/java')
        generatedSourceDirs += file('src/gen/main/grpc')
    }
}
